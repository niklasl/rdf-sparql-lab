prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
prefix : <http://purl.org/net/schemarama#>

construct {

    [] a :Warning;
        :message ?message;
        :implicated (?term ?thing ?doc) .

} where {

    {
        graph ?doc { ?thing ?term ?value }
        filter not exists { ?term a rdf:Property }
        bind("Property [1] not found in schema.org (used for [2] in [3])"@en as ?message)
    } union {
        graph ?doc { ?thing a ?term }
        filter not exists { ?term a rdfs:Class }
        bind("Class [1] not found in schema.org (used for [2] in [3])"@en as ?message)
    }
    filter(strStarts(str(?term), str(schema:)))

}
